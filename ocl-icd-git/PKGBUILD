# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <blacksky3@tuta.io> <https://github.com/blacksky3>
#Credits: Laurent Carlier <lordheavym@gmail.com>
#Credits: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=ocl-icd-git
pkgdesc='OpenCL ICD Bindings (git version)'
pkgver=2.3.1.r0.g1165dee
pkgrel=1
arch=(x86_64)
url='https://github.com/OCL-dev/ocl-icd'
license=(Apache-2.0)
makedepends=(ruby mesa xmlto xmltoman python-xmltodict asciidoc opencl-headers ninja make cmake git gcc gcc-libs)
depends=(glibc)
optdepends=('opencl-driver: packaged opencl driver')
conflicts=(libcl ocl-icd khronos-ocl-icd khronos-ocl-icd-git)
replaces=(libcl)
provides=(opencl-icd-loader ocl-icd ocl-icd-git khronos-ocl-icd khronos-ocl-icd-git)
source=(git+https://github.com/OCL-dev/ocl-icd.git)
md5sums=(SKIP)

pkgver(){
 cd ${srcdir}/ocl-icd

 # cutting off 'foo-' prefix that presents in the git tag
 git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cd ${srcdir}/ocl-icd

  autoreconf -fiv

  ./configure --prefix=/usr

  make -j$(nproc)
}

package_ocl-icd-git(){
  make DESTDIR="$pkgdir" -C ${srcdir}/ocl-icd/ install

  # install licence
  install -Dm644 ${srcdir}/ocl-icd/COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
