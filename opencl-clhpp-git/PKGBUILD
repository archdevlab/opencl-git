#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Laurent Carlier <lordheavym@gmail.com>

pkgname=opencl-clhpp-git
pkgdesc='OpenCL C++ header files (git version)'
pkgver=2.0.16.r4.g61a5c9a
pkgrel=1
arch=(any)
url='https://github.com/KhronosGroup/OpenCL-CLHPP'
license=(Apache-2.0)
makedepends=(cmake doxygen graphviz opencl-headers make git ruby)
depends=(opencl-headers)
conflicts=(opencl-clhpp)
provides=(opencl-clhpp opencl-clhpp-git)
source=(git+https://github.com/KhronosGroup/OpenCL-CLHPP.git)

pkgver(){
  cd "${srcdir}"/OpenCL-CLHPP
  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd "${srcdir}"/OpenCL-CLHPP
  git submodule update --init --recursive
}

build(){
  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/OpenCL-CLHPP \
  -G "Unix Makefiles" \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_LIBDIR=lib \
  -D CMAKE_INSTALL_SYSCONFDIR=/etc \
  -D CMAKE_INSTALL_DATADIR=share \
  -D CMAKE_INSTALL_INCLUDEDIR=include \
  -D BUILD_DOCS=ON \
  -D BUILD_EXAMPLES=OFF \
  -D BUILD_TESTS=OFF \
  -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build docs
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  # installing docs
  install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"/
  cp -r "${srcdir}"/build/docs/html/* "${pkgdir}"/usr/share/doc/"${pkgname}"/

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/OpenCL-CLHPP/LICENSE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
