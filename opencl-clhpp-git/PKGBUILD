# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <blacksky3@tuta.io> <https://github.com/blacksky3>
#Credits: Laurent Carlier <lordheavym@gmail.com>

pkgname=opencl-clhpp-git
pkgdesc='OpenCL C++ header files'
pkgver=2.0.16.r4.g61a5c9a
pkgrel=1
arch=(x86_64)
url='https://github.com/KhronosGroup/OpenCL-CLHPP'
license=(Apache-2.0)
makedepends=(cmake doxygen graphviz ninja git gcc gcc-libs python ruby)
depends=(opencl-headers)
conflicts=(opencl-clhpp)
provides=(opencl-clhpp opencl-clhpp-git)
source=(git+https://github.com/KhronosGroup/OpenCL-CLHPP.git)
md5sums=(SKIP)

pkgver(){
  cd ${srcdir}/OpenCL-CLHPP

  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare(){
  cd ${srcdir}/OpenCL-CLHPP
  git submodule update --init --recursive
}

build(){
  cd ${srcdir}/OpenCL-CLHPP

  rm -rf build

  cmake -H. -G Ninja -Bbuild \
  -DCMAKE_C_FLAGS=-m64 \
  -DCMAKE_CXX_FLAGS=-m64 \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DBUILD_DOCS=ON \
  -DBUILD_EXAMPLES=OFF \
  -DBUILD_TESTS=OFF

  ninja -C build docs
}

package(){
  DESTDIR="$pkgdir" ninja -C ${srcdir}/OpenCL-CLHPP/build/ install

  # installing docs
  install -dm755 "${pkgdir}"/usr/share/doc/$pkgname/
  cp -r OpenCL-CLHPP/build/docs/html/* "${pkgdir}"/usr/share/doc/$pkgname/

  # install licence
  install -Dm644 "${srcdir}"/OpenCL-CLHPP/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
