#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: St√©phane Gaudreault <stephane@archlinux.org>
#Credits: Sylvain HENRY <hsyl20@yahoo.fr>

pkgname=opencl-headers-git
pkgdesc='OpenCL (Open Computing Language) header files (git version)'
pkgver=2022.01.04.r3.gdcd5bed
pkgrel=1
arch=(any)
url='https://github.com/KhronosGroup/OpenCL-Headers'
license=(Apache-2.0)
makedends=(cmake make python git)
conflicts=(opencl-headers)
provides=(opencl-headers opencl-headers-git)
optdepends=('opencl-clhpp: C++ support')
source=(git+https://github.com/KhronosGroup/OpenCL-Headers.git)

pkgver(){
  cd "${srcdir}"/OpenCL-Headers
  # cutting off 'foo-' prefix that presents in the git tag
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  rm -rf "${srcdir}"/build

  cmake -B "${srcdir}"/build -S "${srcdir}"/OpenCL-Headers \
  -G "Unix Makefiles" \
  -D CMAKE_BUILD_TYPE=Release \
  -D CMAKE_INSTALL_PREFIX=/usr \
  -D CMAKE_INSTALL_LIBDIR=lib \
  -D CMAKE_INSTALL_SYSCONFDIR=/etc \
  -D CMAKE_INSTALL_DATADIR=share \
  -D CMAKE_INSTALL_INCLUDEDIR=include \
  -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package() {
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/OpenCL-Headers/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
